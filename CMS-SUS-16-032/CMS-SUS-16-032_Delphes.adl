#info analysis
#  title "Search for the pair production of third-generation squarks with two-body decays to 
a bottom or charm quark and a neutralino in proton-proton collisions at sqrts = 13 TeV"
#  experiment CMS
#  id SUS-16-032
#  publication Phys. Lett. B 778 (2018) 263
#  sqrtS 13.0
#  lumi 35.9
#  arXiv 1707.07274  
#  hepdata 
#  doi 10.1016/j.physletb.2018.01.012

# OBJECTS
object jets
  take Jet
  select pT > 25  
  select abs(Eta) < 2.4

object bjets
  take jets
  select BTag == 1

object cjets
  take jets
  # They have a 2D tagging algorithm (vs light and b jets)
  # c tagging doesn't exist in Delphes, but written here just as an ad hoc property
  # Can match with genparticles, or try to make JetFlavor work in Delphes
  # select cTag == 1
  # For the time being, select all the non-b jets:
  select bTag != 1

object muons
  take Muon
  select pT > 10
  select abs(eta) < 2.4
  select D0 < 2
  # relative isolation Irel < 0.10

object electrons
  take Electron
  select pT > 10
  select abs(eta) < 2.4
  select D0 < 2
  # relative isolation Irel < 0.21

#object vetotracks
# no isolation in Delphes, therefore cannot readily do it in Delphes
#  take Track
#  select pT > 10
#  select abs(eta) < 2.5
#  select sumPt / pT < 0.1 # R = 0.3
  
object MET
  take MissingET

# VARIABLES
define MTj1 = sqrt( 2*jets[0].pT * MET*(1-cos(MET.phi - jets[0].phi )))
define MTj2 = sqrt( 2*jets[1].pT * MET*(1-cos(MET.phi - jets[1].phi )))
define MCT = 2 * jets[0].pT * jets[1] * (1 + cos( dphi( jets[0], jets[1] )))
define dphimin2j = min(dphi(jets[0:1], MET))
define dphimin = min(dphi(jets[0:2], MET))
define HT01 = jets[0] + jets[1]
define HTbc = sum(bjets.pT) + sum(cjets.pT)

# EVENT SELECTION
# The noncompressed search regions
region noncompressed
  select size(jets) [] 2 4
  #reject size(jets) > 4 ? jets[4].pT > 75
  select size(electrons) + size(muons) == 0
  select jets[0].pT > 100
  select jets[0].BTag == 1
  select jets[1].pT > 75
  select jets[1].BTag ==    1
  select size(jets) > 2 ? jets[2].pT > 30
  select size(jets) > 3 ? jets[3].pT > 30
  select MET.pT > 250
  select size(jets) == 2 ? dphimin2 > 0.4 : dphimin > 0.4  
  select min(MTj1, MTj2) > 250
  select MCT > 150

region noncompressedHT1
  select noncompressed
  select HT01 [] 200 500
  bin MCT 150 250 350 450 

region noncompressedHT2
  select noncompressed
  select HT01 [] 500 1000
  bin MCT 150 250 350 450 600  

region noncompressedHT3
  select noncompressed
  select HT01 > 1000
  bin MCT 150 250 350 450 600 800

# The compressed search regions
region compressed
  select size(jets) [] 2 4
  #reject size(jets) > 4 ? jets[4].pT > 75
  select size(electrons) + size(muons) == 0
  select jets[0].pT > 100
  select jets[0].BTag == 0 and jets[0].cTag == 0
  select jets[1].BTag == 0 and jets[1].cTag == 1
  select MET.pT > 250
  select size(jets) == 2 ? dphimin2 > 0.4 : dphimin > 0.4  
  select dphimin > 0.4  
  select jets[1].pT > 50 ? (pT(jets[0] + jets[1]) + MET.pT) / MET.pT < 0.5 : (jets[0].pT + MET.pT) / MET.pT < 0.5 

region compressednb1
  select compressed
  select size(bjets) == 1
  select HTbc < 100
  bin MET.pT 250 300 500 750 1000

region compressednb2
  select compressed
  select size(bjets) == 1
  bin MET.pT [] 250 300 and HTbc < 100
  bin MET.pT [] 250 300 and HTbc [] 100 200
  bin MET.pT [] 300 500 and HTbc < 100
  bin MET.pT [] 300 500 and HTbc [] 100 200
  bin MET.pT > 500 and HTbc < 100
  bin MET.pT > 500 and HTbc [] 100 200

region compressednc1
  select compressed
  select size(cjets) == 1
  select HTbc < 100
  bin MET.pT 250 300 500 750 1000

region compressednc2
  select compressed
  select size(cjets) == 1
  bin MET.pT [] 250 300 and HTbc < 100
  bin MET.pT [] 250 300 and HTbc [] 100 200
  bin MET.pT [] 300 500 and HTbc < 100
  bin MET.pT [] 300 500 and HTbc [] 100 200
  bin MET.pT [] 500 750 and HTbc < 100
  bin MET.pT [] 500 750 and HTbc [] 100 200
  bin MET.pT > 750 and HTbc < 100
  bin MET.pT > 750 and HTbc [] 100 200

region compressednbnc0
  # this region also requires 0 secondary vertices
  select compressed
  select size(bjets) + size(cjets) == 0
  bin MET.pT 300 500 750 1000 1250

# There is another region like compressednbnc0 which has >0 secondary vertices




