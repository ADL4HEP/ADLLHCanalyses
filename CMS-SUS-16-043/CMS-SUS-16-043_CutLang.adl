#info analysis
#  title "Search for electroweak production of charginos and neutralinos in WH events in 
# proton-proton collisions at sqrt(s) = 13TeV"
#  experiment CMS
#  id SUS-16-043
#  publication JHEP 11 (2017) 029
#  sqrtS 13.0
#  lumi 35.9
#  arXiv 1706.09933
#  hepdata 
#  doi 10.1007/JHEP11(2017)029

# OBJECTS
object muons
  take Muon
  select pT(Muon) > 25
  select abs(eta(Muon)) < 2.1
  #select dxy < 2 ?
  # isolation, pTsum < 5, DR < 0.3

object electrons
  take Electron
  select pT(Electron) > 30
  select abs(eta(Electron)) < 1.44
  #select dxy < 2 ?
  # isolation, pTsum < 5, DR < 0.3

object leptons : Union(electrons, muons)

# We don't get relative isolation in Delphes
object vetoelectrons
  take Electron
  select pT(Electron) [] 5 30
  # pTsum / pT < 0.1 where where pTsum calculated from 
  # DR = 0.2 for electron.pT =< 50
  # DR = max(0.5, 10/electron.pT) for electron.pT > 50

object vetomuons
  take Muon
  select pT(Muon) [] 5 25
  # pTsum / pT < 0.1 where pTsum is calculated from 
  # DR = 0.2 for muon.pT =< 50
  # DR = max(0.5, 10/muon.pT) for muon.pT > 50

object vetoleptons : Union( vetoelectrons, vetomuons)

object vetotaus
  take Jet
  select TauTag(Jet) == 1
  select pT(Jet) > 20

object jets
  take Jet 
  select pT(Jet) > 30  
  select abs(Eta(Jet)) < 2.4
  select dR(Jet, leptons) < 0.4 

object bjets
  take jets
  select BTag(jets) == 1
  
# Can't do tracks in Delphes.  Not necessary for overlap study.
#object vetotracks
#  take Track t
#  reject dR(t, leptons) < 0.2
#  select pT > 10
#  select size(leptons) > 0 ? leptons[0].q + q == 0
  
# VARIABLES
define MTl = sqrt( 2*pT(leptons[0]) * MET*(1-cos(Phi(METLV_0) - Phi(leptons[0]) )))
define MCT = 2 * pT(bjets[0]) * pT(bjets[1]) * (1 + cos( dPhi( bjets[0], bjets[1] )))

# EVENT SELECTION
# (see Table 1 in paper)
region preselection
  select Size(electrons) + Size(muons) >= 0
  select Size(vetoelectrons) + Size(vetomuons) >= 0
  select Size(leptons) == 1
  reject Size(vetoleptons) > 1
  # select size(vetotracks) == 0
  select Size(vetotaus) == 0
  select Size(bjets) == 2
  select MET > 125
  select MTl > 50

region signal
  preselection
  select m(bjets[0] bjets[1]) [] 90 150
  select MTl > 150
  select MCT > 170
  bin MET 125 200
  
