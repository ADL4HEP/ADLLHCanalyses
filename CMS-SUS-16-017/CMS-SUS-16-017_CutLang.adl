# arxiv:1812.06302, CMS SUSY razor

#info analysis
# Details about experiment
#  experiment CMS
#  id SUS-16-017
#  title Inclusive search for supersymmetry in pp collisions at sqrt(s)=13 TeV using razor 
# variables and boosted object identification in zero and one lepton final states
#  publication JHEP 1903 (2019) 031
#  sqrtS 13.0
#  lumi 35.9
#  arXiv 1812.06302
#  hepdata 
#  doi 10.1007/JHEP03(2019)031

### OBJECT SELECTIONS

# AK4 jets
object AK4jets : JET
  select {JET_}Pt > 30 
  select {JET_}AbsEta < 2.4

# AK8 jets
object AK8jets : FJET
  select {FJET_}Pt > 200
  select {FJET_}Eta < 2.4

# b-tagged jets - loose
object bjetsLoose : AK4jets
  select {AK4jets_}btagDeepB > 0.152

# b-tagged jets - medium
object bjetsMedium : AK4jets
  select {AK4jets_ }btagDeepB > 0.4941

# b-tagged jets - tight
object bjetsTight : AK4jets
  select {AK4jets_}btagDeepB > 0.8001

# W jets - mass-tagged
object WjetsMasstag : AK8jets
  select {AK8jets_}msoftdrop [] 65 105

# W jets - W-tagged
object Wjets : WjetsMasstag
  select {WjetsMasstag_}tau2 / {WjetsMasstag_}tau1 <= 0.4

# W jets - anti-tagged
object WjetsAntitag : WjetsMasstag
  select {WjetsMasstag_}tau2 / {WjetsMasstag_}tau1 > 0.4

# top jets - mass-tagged
object topjetsMasstag : AK8jets
  select {AK8jets_}Pt > 400
  select {AK8jets_}msoftdrop [] 105 210

# top jets - mass-tagged, subjet b-antitagged
object topjetsMasstag0b : topjetsMasstag
  select {topjetsMasstag_ }btagDeepB < 0.1522  

# top jets - top-tagged, subjet b-tagged
object topjets : topjetsMasstag
  select {topjetsMasstag_}btagDeepB >= 0.1522 
  select {topjetsMasstag_}tau3 / {topjetsMasstag_}tau2 < 0.46   ##########put this into FATJET

# top jets - anti-tagged
object topjetsAntitag : topjetsMasstag
  select {topjetsMasstag_}btagDeepB < 0.1522
  select {topjetsMasstag_}tau3 / {topjetsMasstag_}tau2 >= 0.46

# muons - veto
object muonsVeto : MUO
  select {MUO_}Pt > 5
  select {MUO_}AbsEta < 2.4
  select {MUO_}softId == 1
  select {MUO_}miniPFRelIsoAll < 0.2
  select abs({MUO_}dxy) < 0.2
  select abs({MUO_}dz) < 0.5  

# muons - select
object muonsSel : MUO
  select {MUO_}Pt > 10
  select {MUO_}AbsEta < 2.4
  select {MUO_}miniPFRelIsoAll < 0.15
  select abs({MUO_}dxy) < 0.05
  select abs({MUO_}dz) < 0.1

# electrons - veto
object electronsVeto : ELE
  select {ELE_}Pt > 5
  select {ELE_}AbsEta < 2.5
  select {ELE_}miniPFRelIsoAll < 0.1
  select abs({ELE_}dxy) < 0.05
  select abs({ELE_}dz) < 0.1

# electrons - select
object electronsSel : ELE
  select {ELE_}Pt > 10
  select {ELE_}AbsEta < 2.5 
  select {ELE_}AbsEta  ][ 1.442 1.556
  select {ELE_}miniPFRelIsoAll < 0.1
  select abs({ELE_}dxy) < 0.05
  select abs({ELE_}dz) < 0.1

# taus - veto
object tausVeto : TAU 
  select {Tau_}Pt > 18    
  select {Tau_}AbsEta < 2.5  
  select {Tau_}dMVAnewDM2017v2 >= 4

# photons - select
object photons : PHO
  select {PHO_}Pt > 80
  select {PHO_}AbsEta < 2.5

# jets - no photon
object AK4jetsNopho : AK4jets 
  select dR(AK4jets_, photons_ ) >=0.4 OR {photons_}Pt/{AK4jets_}Pt ][ 0.5 2.0
#  reject dR(AK4jets_, photons) < 0.4 AND photons.pt/j.pt [] 0.5 2.0

# Megajets are 4-vectors, each consisting of multiple jet.  
# Each event with njets > 0 is partitioned into 2 megajets.
object megajets : AK4jets
  select fmegajets(AK4jets) == 2   

object megajetsNopho : AK4jetsNopho
  select fmegajets(AK4jetsNopho) == 2   

### EVENT VARIABLES
define MR : fMR(megajets)
define Rsq : (fMTR(megajets, MET) / MR )^0.5
define dphimegajets : dPhi(megajets[0], megajets[1])
define dphimegajetsNopho : dPhi(megajetsNopho[0], megajetsNopho[1])

define METLVe   : METLV_0 electronsVeto_0
define METLVm   : METLV_0 muonsVeto_0
define METLVee  : METLV_0 electronsVeto_0  electronsVeto_1
define METLVmm  : METLV_0 muonsVeto_0  muonsVeto_1
define METLVpho : METLV_0 photons[0]

define Rsqe : (fMTR(megajets, METLVe) / MR)^0.5
define Rsqm : (fMTR(megajets, METLVm) / MR)^0.5
define Rsqee : sqrt(fMTR(megajets, METLVee) / MR)
define Rsqmm : sqrt(fMTR(megajets, METLVmm) / MR)
define MRNopho : fMR(megajetsNopho)
define Rsqpho : sqrt(fMTR(megajetsNopho, METLVpho) / MRNopho)
define MTe : sqrt( 2*{electronsVeto_0}Pt *MET*(1-cos( {METLV_0}Phi - {electronsVeto_0}Phi )))
define MTm : sqrt( 2*{muonsVeto_0}Pt *MET*(1-cos( {METLV_0}Phi - {muonsVeto_0}Phi )))
define mZ : 91.187

# EVENT SELECTION
# Boosted categories

# Boost pre-selection cuts
region preselection
  select ALL   # This is only to see the initial event count             
  select Size(AK4jets) >= 3   
  select Size(AK8jets) >= 1   
  select Size(megajets) == 2 
  select MR > 800  
  select Rsq > 0.08 

region WcategorySR  
  preselection
  select Size(electronsVeto) + Size(muonsVeto) == 0
  select Size(tausVeto) == 0
  select Size(bjetsMedium) >= 1
  select Size(Wjets) >= 1
  #histo hWjPT, "Wjets Pt GeV", 10, 0, 500, {Wjets_0}Pt
  select dphimegajets < 2.8

region WcategoryCRQ
  preselection
  select Size(electronsVeto) + Size(muonsVeto) == 0
  select Size(tausVeto) == 0
  select Size(WjetsAntitag) >= 1
  select Size(bjetsLoose) == 0
  select dphimegajets >= 2.8

region WcategoryCRT
  preselection
  select Size(electronsVeto) + Size(muonsVeto) == 1 
  select Size(Wjets) >= 1
  select dphimegajets < 2.8
  select Size(muonsVeto) == 1 ? MTm < 100 : MTe < 100
  select Size(bjetsMedium) >= 1

region WcategoryCRW
  preselection
  select Size(muonsVeto) + Size(electronsVeto) == 1
  select Size(WjetsMasstag) >= 1
  select dphimegajets < 2.8
  select Size(muonsVeto) == 1 ? MTm [] 30 100 : MTe [] 30 100
  select Size(bjetsLoose) == 0

region WcategoryCRL
  select Size(AK4jets) >= 3
  select Size(AK8jets) >= 1
  select Size(megajets) == 2
  select MR > 800
  select Size(muonsVeto) + Size(electronsVeto) == 1
  select Size(muonsVeto) == 1 ? Rsqm > 0.08 : Rsqe > 0.08
  select Size(WjetsMasstag) >= 1
  select dphimegajets < 2.10
  select Size(muonsVeto) == 1 ? MTm [] 30 100 : MTe [] 30 100
  select Size(bjetsLoose) == 0

region WcategoryCRZ
  select Size(AK4jets) >= 3
  select Size(AK8jets) >= 1
  select Size(megajets) == 2
  select MR > 800
  select (Size(muonsSel) == 2 AND Size(electronsVeto) == 0) OR (Size(electronsSel) == 2 AND Size(muonsVeto) == 0)
  select Size(muonsSel) == 2 ? {muonsSel[0]}q + {muonsSel[1]}q == 0 : {electronsSel[0]}q + {electronsSel[1]}q == 0 
  select Size(muonsSel) == 2 ? Abs({muonsSel[0] muonsSel[1]}m - mZ) < 10 : Abs({electronsSel[0] electronsSel[1]}m - mZ) < 10 
  select Size(muonsSel) == 2 ? Rsqmm > 0.08 : Rsqee > 0.08 
  select Size(WjetsMasstag) >= 1
  select dphimegajets < 2.8

region WcategoryCRG
  select Size(photons) == 1
  select Size(AK4jetsNopho) >= 3
  select Size(AK8jets) >= 1
  select Size(electronsVeto) + Size(muonsVeto) == 0
  select Size(tausVeto) == 0
  select Size(megajetsNopho) == 2
  select MRNopho > 800
  select Rsqpho > 0.08
  select Size(WjetsMasstag) >= 1
  select dphimegajetsNopho < 2.8

# Top category signal and control regions
region TopcategorySR
  preselection
  select Size(electronsVeto) + Size(muonsVeto) == 0
  select Size(tausVeto) == 0
  select Size(topjets) >= 1
  select dphimegajets < 2.8

region TopcategoryCRQ
  preselection
  select Size(electronsVeto) + Size(muonsVeto) == 0
  select Size(tausVeto) == 0
  select Size(topjetsAntitag) >= 1
  select dphimegajets >= 2.8
  select Size(bjetsLoose) == 0

region TopcategoryCRT
  preselection
  select Size(electronsVeto) + Size(muonsVeto) == 1 
  select Size(topjets) >= 1
  select dphimegajets < 2.8
  select Size(muonsVeto) == 1 ? MTm < 100 : MTe < 100

region TopcategoryCRW
  preselection
  select Size(muonsVeto) + Size(electronsVeto) == 1
  select Size(topjetsMasstag0b) >= 1
  select dphimegajets < 2.8
  select Size(muonsVeto) == 1 ? MTm [] 30 100 : MTe [] 30 100
  select Size(bjetsLoose) == 0

region TopcategoryCRL
  select Size(AK4jets) >= 3
  select Size(AK8jets) >= 1
  select Size(megajets) == 2
  select MR > 800
  select Size(muonsVeto) + Size(electronsVeto) == 1
  select Size(muonsVeto) == 1 ? Rsqm > 0.08 : Rsqe > 0.08
  select Size(topjetsMasstag0b) >= 1
  select dphimegajets < 2.10
  select Size(muonsVeto) == 1 ? MTm [] 30 100 : MTe [] 30 100
  select Size(bjetsLoose) == 0

region TopcategoryCRZ
  select Size(AK4jets) >= 3
  select Size(AK8jets) >= 1
  select Size(megajets) == 2
  select MR > 800
  select (Size(muonsSel) == 2 AND Size(electronsVeto) == 0) OR (Size(electronsSel) == 2 AND Size(muonsVeto) == 0)
  select Size(muonsSel) == 2 ? {muonsSel_0}q + {muonsSel_1}q == 0 : {electronsSel_0}q + {electronsSel_1}q == 0 
  select Size(muonsSel) == 2 ? Abs({muonsSel_0 muonsSel_1}m - mZ) < 10 : Abs({electronsSel_0 electronsSel_1}m -  mZ) < 10
  select Size(muonsSel) == 2 ? Rsqmm > 0.08 : Rsqee > 0.08 
  select Size(topjetsMasstag) >= 1
  select dphimegajets < 2.8

region TopcategoryCRG
  select Size(photons) == 1
  select Size(AK4jetsNopho) >= 3
  select Size(AK8jets) >= 1
  select Size(electronsVeto) + Size(muonsVeto) == 0
  select Size(tausVeto) == 0
  select Size(megajetsNopho) == 2
  select MRNopho > 800
  select Rsqpho > 0.08
  select Size(topjetsMasstag) >= 1
  select dphimegajetsNopho < 2.8
