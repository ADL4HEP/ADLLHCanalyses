# arxiv:1812.06302, CMS SUSY razor

#info analysis
# Details about experiment
#  experiment CMS
#  id SUS-16-017
#  title Inclusive search for supersymmetry in pp collisions at sqrt(s)=13 TeV using razor 
# variables and boosted object identification in zero and one lepton final states
#  publication JHEP 1903 (2019) 031
#  sqrtS 13.0
#  lumi 35.9
#  arXiv 1812.06302
#  hepdata 
#  doi 10.1007/JHEP03(2019)031

### OBJECT SELECTIONS

# AK4 jets
object AK4jets
  take Jet
  select pT(Jet) > 30
  select abs(Eta(Jet)) < 2.4

# AK8 jets
object AK8jets
  take FJET
  select pT(FJET) > 200
  select abs(Eta(FJET)) < 2.4

# b-tagged jets - loose
object bjetsLoose
  take AK4jets
  select btagDeepB(AK4jets) > 0.152

# b-tagged jets - medium
object bjetsMedium
  take AK4jets
  select btagDeepB(AK4jets) > 0.4941

# b-tagged jets - tight
object bjetsTight
  take AK4jets
  select btagDeepB(AK4jets) > 0.8001

# W jets - mass-tagged
object WjetsMasstag
  take AK8jets
  select msoftdrop(AK8jets) [] 65 105

# W jets - W-tagged
object Wjets
  take WjetsMasstag
  select tau2(WjetsMasstag) / tau1(WjetsMasstag) <= 0.4

# W jets - anti-tagged
object WjetsAntitag
  take WjetsMasstag
  select tau2(WjetsMasstag) / tau1(WjetsMasstag) > 0.4

# top jets - mass-tagged
object topjetsMasstag
  take AK8jets
  select pT(AK8jets) > 400
  select msoftdrop(AK8jets) [] 105 210

# top jets - mass-tagged, subjet b-antitagged
object topjetsMasstag0b
  take topjetsMasstag
  select btagDeepB(topjetsMasstag) < 0.1522

# top jets - top-tagged, subjet b-tagged
object topjets
  take topjetsMasstag
  select btagDeepB(topjetsMasstag) >= 0.1522
  select tau3(topjetsMasstag) / tau2(topjetsMasstag) < 0.46   ##########put this into FATJET

# top jets - anti-tagged
object topjetsAntitag : topjetsMasstag
  select btagDeepB(topjetsMasstag) < 0.1522
  select tau3(topjetsMasstag) / tau2(topjetsMasstag) >= 0.46

# muons - veto
object muonsVeto
  take Muon
  select pT(Muon) > 5
  select Abs(Eta(Muon)) < 2.4
  select softId(Muon) == 1
  select miniPFRelIsoAll(Muon) < 0.2
  select abs(dxy(Muon)) < 0.2
  select abs(dz(Muon)) < 0.5

# muons - select
object muonsSel
  take Muon
  select pT(Muon) > 10
  select Abs(Eta(Muon)) < 2.4
  select miniPFRelIsoAll(Muon) < 0.15
  select abs(dxy(Muon)) < 0.05
  select abs(dz(Muon)) < 0.1

# electrons - veto
object electronsVeto
  take Electron
  select pT(Electron) > 5
  select Abs(Eta(Electron)) < 2.5
  select miniPFRelIsoAll(Electron) < 0.1
  select abs(dxy(Electron)) < 0.05
  select abs(dz(Electron)) < 0.1

# electrons - select
object electronsSel
  take Electron
  select pT(Electron) > 10
  select Abs(Eta(Electron)) < 2.5
  select Abs(Eta(Electron)) ][ 1.442 1.556
  select miniPFRelIsoAll(Electron) < 0.1
  select abs(dxy(Electron)) < 0.05
  select abs(dz(Electron)) < 0.1

# taus - veto
object tausVeto
  take Tau
  select pT(Tau) > 18
  select Abs(Eta(Tau)) < 2.5
  select dMVAnewDM2017v2(Tau) >= 4

# photons - select
object photons
  take Photon
  select pT(Photon) > 80
  select Abs(Eta(Photon)) < 2.5

# jets - no photon
object AK4jetsNopho
  take AK4jets
  select dR(AK4jets_, photons_ ) >=0.4 OR {photons_}Pt/{AK4jets_}Pt ][ 0.5 2.0

# Megajets are 4-vectors, each consisting of multiple jet.  
# Each event with njets > 0 is partitioned into 2 megajets.
object megajets
  take AK4jets
  select fmegajets(AK4jets) == 2   

object megajetsNopho
  take AK4jetsNopho
  select fmegajets(AK4jetsNopho) == 2   

### EVENT VARIABLES
define MR : fMR(megajets)
define Rsq : (fMTR(megajets, MET) / MR )^0.5
define dphimegajets : dPhi(megajets[0], megajets[1])
define dphimegajetsNopho : dPhi(megajetsNopho[0], megajetsNopho[1])

define METLVe   = METLV[0] electronsVeto[0]
define METLVm   = METLV[0] muonsVeto[0]
define METLVee  = METLV[0] electronsVeto[0]  electronsVeto[1]
define METLVmm  = METLV[0] muonsVeto[0]  muonsVeto[1]
define METLVpho = METLV[0] photons[0]

define Rsqe = (fMTR(megajets, METLVe) / MR)^0.5
define Rsqm = (fMTR(megajets, METLVm) / MR)^0.5
define Rsqee = sqrt(fMTR(megajets, METLVee) / MR)
define Rsqmm = sqrt(fMTR(megajets, METLVmm) / MR)
define MRNopho = fMR(megajetsNopho)
define Rsqpho = sqrt(fMTR(megajetsNopho, METLVpho) / MRNopho)
define MTe = sqrt( 2*pT(electronsVeto[0]) *MET*(1-cos( Phi(METLV[0]) - Phi(electronsVeto[0]) )))
define MTm = sqrt( 2*pT(muonsVeto[0]) *MET*(1-cos( Phi(METLV[0]) - Phi(muonsVeto[0]) )))
define mZ = 91.187

# EVENT SELECTION
# Boosted categories

# Boost pre-selection cuts
region preselection
  select ALL   # This is only to see the initial event count             
  select Size(AK4jets) >= 3   
  select Size(AK8jets) >= 1   
  select Size(megajets) == 2 
  select MR > 800  
  select Rsq > 0.08 

region WcategorySR  
  preselection
  select Size(electronsVeto) + Size(muonsVeto) == 0
  select Size(tausVeto) == 0
  select Size(bjetsMedium) >= 1
  select Size(Wjets) >= 1
  select dphimegajets < 2.8

region WcategoryCRQ
  preselection
  select Size(electronsVeto) + Size(muonsVeto) == 0
  select Size(tausVeto) == 0
  select Size(WjetsAntitag) >= 1
  select Size(bjetsLoose) == 0
  select dphimegajets >= 2.8

region WcategoryCRT
  preselection
  select Size(electronsVeto) + Size(muonsVeto) == 1 
  select Size(Wjets) >= 1
  select dphimegajets < 2.8
  select Size(muonsVeto) == 1 ? MTm < 100 : MTe < 100
  select Size(bjetsMedium) >= 1

region WcategoryCRW
  preselection
  select Size(muonsVeto) + Size(electronsVeto) == 1
  select Size(WjetsMasstag) >= 1
  select dphimegajets < 2.8
  select Size(muonsVeto) == 1 ? MTm [] 30 100 : MTe [] 30 100
  select Size(bjetsLoose) == 0

region WcategoryCRL
  select Size(AK4jets) >= 3
  select Size(AK8jets) >= 1
  select Size(megajets) == 2
  select MR > 800
  select Size(muonsVeto) + Size(electronsVeto) == 1
  select Size(muonsVeto) == 1 ? Rsqm > 0.08 : Rsqe > 0.08
  select Size(WjetsMasstag) >= 1
  select dphimegajets < 2.10
  select Size(muonsVeto) == 1 ? MTm [] 30 100 : MTe [] 30 100
  select Size(bjetsLoose) == 0

region WcategoryCRZ
  select Size(AK4jets) >= 3
  select Size(AK8jets) >= 1
  select Size(megajets) == 2
  select MR > 800
  select (Size(muonsSel) == 2 AND Size(electronsVeto) == 0) OR (Size(electronsSel) == 2 AND Size(muonsVeto) == 0)
  select Size(muonsSel) == 2 ? q(muonsSel[0]) + q(muonsSel[1]) == 0 : q(electronsSel[0]) + q(electronsSel[1]) == 0
  select Size(muonsSel) == 2 ? Abs({muonsSel[0] muonsSel[1]}m - mZ) < 10 : Abs({electronsSel[0] electronsSel[1]}m - mZ) < 10 
  select Size(muonsSel) == 2 ? Rsqmm > 0.08 : Rsqee > 0.08 
  select Size(WjetsMasstag) >= 1
  select dphimegajets < 2.8

region WcategoryCRG
  select Size(photons) == 1
  select Size(AK4jetsNopho) >= 3
  select Size(AK8jets) >= 1
  select Size(electronsVeto) + Size(muonsVeto) == 0
  select Size(tausVeto) == 0
  select Size(megajetsNopho) == 2
  select MRNopho > 800
  select Rsqpho > 0.08
  select Size(WjetsMasstag) >= 1
  select dphimegajetsNopho < 2.8

# Top category signal and control regions
region TopcategorySR
  preselection
  select Size(electronsVeto) + Size(muonsVeto) == 0
  select Size(tausVeto) == 0
  select Size(topjets) >= 1
  select dphimegajets < 2.8

region TopcategoryCRQ
  preselection
  select Size(electronsVeto) + Size(muonsVeto) == 0
  select Size(tausVeto) == 0
  select Size(topjetsAntitag) >= 1
  select dphimegajets >= 2.8
  select Size(bjetsLoose) == 0

region TopcategoryCRT
  preselection
  select Size(electronsVeto) + Size(muonsVeto) == 1 
  select Size(topjets) >= 1
  select dphimegajets < 2.8
  select Size(muonsVeto) == 1 ? MTm < 100 : MTe < 100

region TopcategoryCRW
  preselection
  select Size(muonsVeto) + Size(electronsVeto) == 1
  select Size(topjetsMasstag0b) >= 1
  select dphimegajets < 2.8
  select Size(muonsVeto) == 1 ? MTm [] 30 100 : MTe [] 30 100
  select Size(bjetsLoose) == 0

region TopcategoryCRL
  select Size(AK4jets) >= 3
  select Size(AK8jets) >= 1
  select Size(megajets) == 2
  select MR > 800
  select Size(muonsVeto) + Size(electronsVeto) == 1
  select Size(muonsVeto) == 1 ? Rsqm > 0.08 : Rsqe > 0.08
  select Size(topjetsMasstag0b) >= 1
  select dphimegajets < 2.10
  select Size(muonsVeto) == 1 ? MTm [] 30 100 : MTe [] 30 100
  select Size(bjetsLoose) == 0

region TopcategoryCRZ
  select Size(AK4jets) >= 3
  select Size(AK8jets) >= 1
  select Size(megajets) == 2
  select MR > 800
  select (Size(muonsSel) == 2 AND Size(electronsVeto) == 0) OR (Size(electronsSel) == 2 AND Size(muonsVeto) == 0)
  select Size(muonsSel) == 2 ? q(muonsSel[0]) + q(muonsSel[1]) == 0 : q(electronsSel[0]) + q(electronsSel[1]) == 0
  select Size(muonsSel) == 2 ? Abs({muonsSel[0] muonsSel[1]}m - mZ) < 10 : Abs({electronsSel[0] electronsSel[1]}m -  mZ) < 10
  select Size(muonsSel) == 2 ? Rsqmm > 0.08 : Rsqee > 0.08 
  select Size(topjetsMasstag) >= 1
  select dphimegajets < 2.8

region TopcategoryCRG
  select Size(photons) == 1
  select Size(AK4jetsNopho) >= 3
  select Size(AK8jets) >= 1
  select Size(electronsVeto) + Size(muonsVeto) == 0
  select Size(tausVeto) == 0
  select Size(megajetsNopho) == 2
  select MRNopho > 800
  select Rsqpho > 0.08
  select Size(topjetsMasstag) >= 1
  select dphimegajetsNopho < 2.8
