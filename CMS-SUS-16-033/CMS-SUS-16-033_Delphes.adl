info analysis
  title "Search for supersymmetry in multijet events with missing transverse momentum in  proton-proton collisions at 13 TeV"
  experiment CMS
  id SUS-16-033
  publication Phys. Rev. D 96, 032003 (2017)
  sqrtS 13.0
  lumi 35.9
  arXiv 1704.07781  
  hepdata 
  doi 10.1103/PhysRevD.96.032003

object jets
  take Jet
  select pT > 30  
  select abs(Eta) < 2.4

object jetsmht
  take Jet
  select pT > 30  
  select abs(Eta) < 5

object bjets
  take jets
  select BTag == 1

object muons
  take Muon
  select pT > 10
  select abs(Eta) < 2.4
  select dO < 2
  # relative isolation Irel < 0.2

object electrons
  take Electron
  select pT > 10
  select abs(Eta) < 2.5
  select abs(d0) < 2
  # relative isolation Irel < 0.1

object MET
  take MissingET

object MHT
  take MissingET

define HT = sum(jets.pT)
#define MHT = Sum(jetsmht)

# preselection region
region presel
  select size(jets) >= 2
  select HT > 300
  select MHT.pT > 300
  select size(muons) == 0
  select size(electrons) == 0
  #select size(tracks) == 0
  select dPhi(jets[0], MHT) > 0.5  
  select dPhi(jets[1], MHT) > 0.5  
  select size(jets) >= 3 ? dPhi(jets[2], MHT) > 0.3
  select size(jets) >= 4 ? dPhi(jets[3], MHT) > 0.3

# Aggregate search regions as shown in Table 3 of the paper:
region SR1
  select presel
  select size(jets) >= 2
  select size(bjets) == 0
  select HT >= 500
  select MHT.pT >= 500

region SR2
  select presel
  select size(jets) >= 3
  select size(bjets) == 0
  select HT >= 1500
  select MHT.pT >= 750

region SR3
  select presel
  select size(jets) >= 5
  select size(bjets) == 0
  select HT >= 500
  select MHT.pT >= 500

region SR4
  select presel
  select size(jets) >= 5
  select size(bjets) == 0
  select HT >= 1500
  select MHT.pT >= 750

region SR5
  select presel
  select size(jets) >= 9
  select size(bjets) == 0
  select HT >= 1500
  select MHT.pT >= 750

region SR6
  select presel
  select size(jets) >= 2
  select size(bjets) >= 2
  select HT >= 500
  select MHT.pT >= 500

region SR7
  select presel
  select size(jets) >= 3
  select size(bjets) >= 1
  select HT >= 750
  select MHT.pT >= 750

region SR8
  select presel
  select size(jets) >= 5
  select size(bjets) >= 3
  select HT >= 500
  select MHT.pT >= 500

region SR9
  select presel
  select size(jets) >= 5
  select size(bjets) >= 2
  select HT >= 1500
  select MHT.pT >= 750

region SR10
  select presel
  select size(jets) >= 9
  select size(bjets) >= 3
  select HT >= 750
  select MHT.pT >= 750

region SR11
  select presel
  select size(jets) >= 7
  select size(bjets) >= 1
  select HT >= 300
  select MHT.pT >= 300

region SR12
  select presel
  select size(jets) >= 5
  select size(bjets) >= 1
  select HT >= 750
  select MHT.pT >= 750

