# arxiv:1704.03848, ATLAS monophoton

info analysis
# Details about experiment
  experiment ATLAS
  id EXOT-2016-32
  publication Eur.Phys.J. C77 (2017) no.6, 393
  sqrtS 13.0
  lumi 36.1
  arXiv 1704.03848
  hepdata https://www.hepdata.net/record/ins1591328
  doi 10.1140/epjc/s10052-017-4965-8

# OBJECT SELECTIONS

object photons
# Delphes photons
  take Photon
  select PT > 10
  #select |Eta| < 2.37

object tightphotons
  take photons
#  select (|Eta| < 1.37) or (|Eta| [] 1.52 2.37)

object electrons
  take Electron
  select PT > 7
  select |Eta| < 2.47

object muons
  take Muon
  select PT > 6
  select |Eta| < 2.7

object jets 
# Delphes jets
  take Jet
  select PT > 20
  select |Eta| < 4.5

object cleanjets
  take jets j
  reject dR(j, electrons) < 0.2

object cleanelectrons
  take electrons e
  #! still under discussion:
  reject dR(e, cleanjets) < 0.4

object cleanmuons
  take muons m
  reject dR(m, cleanjets) < 0.4

object MET
  take MissingET

object jetsSR
  take cleanjets cj
  select PT > 30
  reject dR(cj, photons) < 0.4
  reject dPhi(cj.Phi, MET.Phi) < 0.4

object scalarHT
  # scalar sum of pT of jets, electrons, photons, muons
  # NB: such variables need to be precisely defined so that
  # adapters know how to compute them
  take ScalarHT

# VARIABLES

# met / sqrt(sumET) should be defined directly, but temporarily using an 
# external function until math operators get implemented in adl2tnm
# define METoverSqrtSumET = MET.PT / sqrt(scalarHT)
#define METoverSqrtSumET = fMEToverSqrtSumET(MET, ScalarHT.HT)

# EVENT SELECTION

region preselection
# Pre-selection cuts
  select jets.size > 1
  select tightphotons.size > 0
  select tightphotons[0].PT > 150
  #select dPhi(tightphotons.Phi, MET.Phi) > 0.4
  #select METoverSqrtSumET > 8.5
  reject jetsSR.size > 1
  select cleanmuons.size == 0
  select cleanelectrons.size == 0

region SRI1
  select preselection
  select MET.PT > 150

region SRI2
  select preselection
  select MET.PT > 225

region SRI3
  select preselection
  select MET.PT > 300

region SRE1
  select preselection
  select MET.PT > 150 and MET.PT < 225

region SRE2
  select preselection
  select MET.PT > 225 and MET.PT < 300  

region lhprocsel  
# Reverse order selection for comparison to LH17 proceedings
  select MET.PT > 150
  select tightphotons.size > 0
  select tightphotons[0].PT > 150
  #select dPhi(tightphotons[0].Phi, MET.Phi) > 0.4
  #select METoverSqrtSumET > 8.5
  reject jetsSR.size > 1
  select cleanmuons.size == 0
  select cleanelectrons.size == 0
  
