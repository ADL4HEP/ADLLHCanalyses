# arxiv:1704.03848, ATLAS monophoton

info analysis
# Details about experiment
  experiment ATLAS
  id EXOT-2016-32
  publication Eur.Phys.J. C77 (2017) no.6, 393
  sqrtS 13.0
  lumi 36.1
  arXiv 1704.03848
  hepdata https://www.hepdata.net/record/ins1591328
  doi 10.1140/epjc/s10052-017-4965-8

# OBJECT SELECTIONS

object photons
  take Photon
  select pT > 10
  select abs(Eta) < 2.37

object tightphotons
  take photons
  select (abs(Eta) < 1.37) or (abs(Eta) [] 1.52 2.37)

object electrons
  take Electron
  select pT > 7
  select abs(Eta) < 2.47

object muons
  take Muon
  select pT > 6
  select abs(Eta) < 2.7

object jets
  take Jet
  select pT > 20
  select abs(Eta) < 4.5

object cleanjets
  take jets j
  select dR(j, electrons) > 0.2

object cleanelectrons
  take electrons e
  select dR(e, cleanjets) > 0.4

object cleanmuons
  take muons m
  select dR(m, cleanjets) > 0.4

object MET
  take MissingET

object jetsSR
  take cleanjets cj
  select pT > 30
  select dR(cj, photons) > 0.4
  select dPhi(cj, MET) > 0.4

  
# VARIABLES

# scalar sum of pT of jets, electrons, photons, muons
define SumET = sum(tightphotons.pT) + sum(jetsSR.pT) + sum(cleanelectrons.pT) + sum(cleanmuons.pT)
define METoverSqrtSumET = MET / sqrt(SumET)

# EVENT SELECTION

region preselection
# Pre-selection cuts
  select size(tightphotons) > 0
  select tightphotons[0].pT > 150
  select size(cleanmuons) == 0
  select size(cleanelectrons) == 0
  select size(jetsSR) <= 1
  select METoverSqrtSumET > 8.5

region SRI1
  preselection
  select MET.MET > 150

region SRI2
  preselection
  select MET.MET > 225

region SRI3
  preselection
  select MET.MET > 300

region SRE1
  preselection
  select MET.MET > 150 and MET.MET < 225

region SRE2
  preselection
  select MET.MET > 225 and MET.MET < 300
  
