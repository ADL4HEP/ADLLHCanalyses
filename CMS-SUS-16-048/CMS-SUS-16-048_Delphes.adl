info analysis
  title "Search for new physics in events with two soft oppositely charged leptons and missing transverse momentum in proton-proton collisions at sqrts = 13 TeV "
  experiment CMS
  id SUS-16-048
  publication Phys. Lett. B 782 (2018) 440
  sqrtS 13.0
  lumi 35.9
  arXiv 1801.01846
  hepdata 
  doi 10.1016/j.physletb.2018.05.062

# ANALYSIS OBJECTS
object muons
  take Muon
  select pT [] 3.5 30
  select abs(eta) < 2.4
  # e.g. select IsolationVarRhoCorr < 0.1
  # Contained in the provided CMS efficiencies

object electrons
  take Electron
  select pT [] 3.5 30
  select abs(eta) < 2.5
  # e.g. select IsolationVarRhoCorr < 0.1
  # Contained in the provided CMS efficiencies

object leptons
  take electrons
  take muons
  
object jets
  take Jet
  select pT > 25  
  select abs(Eta) < 2.4

object bjets
  take jets
  select BTag == 1

object MET
  take MissingET

# EVENT VARIABLES
define dilepton = leptons[0] + leptons[1]
define dielectron = electrons[0] + electrons[1]
define dimuon = muons[0] + muons[1]
define HT = sum(jets.pT)
define MTl1 = sqrt( 2*leptons[0].pT * MET.MET*(1-cos(MET.phi - leptons[0].phi )))
define MTl2 = sqrt( 2*leptons[1].pT * MET.MET*(1-cos(MET.phi - leptons[1].phi )))
define Mtautau = fMtautau(leptons[0], leptons[1], MET)

# EVENT SELECTION

# Dilepton preselection region
region dileptonpresel
  select size(leptons) == 2
  select leptons[0].Pt [] 5 30
  select leptons[1].Pt [] 5 30
  select leptons[0].q * leptons[1].q == -1
  select dilepton.Pt > 3
  select dilepton.m [] 4 50
  select dilepton.m ][ 9 10.5
  select size(muons) == 2 ? Pt(METLV[0]) > 125 : Pt(METLV[0]) > 200
  weight trigger 0.65
  select size(jets) >= 1
  select HT > 100
  select (MET.MET / HT) > 0.6 and (MET.MET / HT) < 1.4
  select size(bjets) == 0
  reject fMTauTau(leptons[0], leptons[1]) [] 0 160
  select MTl1 < 70 AND MTl2 < 70

# Signal bins for EWK gaugino search
region ewkbins
  dileptonpresel
  select Size(electrons) == 2 or Size(muons) == 2
  bin MET.MET [] 125 200 and dilepton.m [] 4 9
  bin MET.MET [] 125 200 and dilepton.m [] 10.5 20
  bin MET.MET [] 125 200 and dilepton.m [] 20 30
  bin MET.MET [] 125 200 and dilepton.m [] 30 50
  bin MET.MET [] 200 250 and dilepton.m [] 4 9
  bin MET.MET [] 200 250 and dilepton.m [] 10.5 20
  bin MET.MET [] 200 250 and dilepton.m [] 20 30
  bin MET.MET [] 200 250 and dilepton.m [] 30 50
  bin MET.MET > 250 and dilepton.m [] 4 9
  bin MET.MET > 250 and dilepton.m [] 10.5 20
  bin MET.MET > 250 and dilepton.m [] 20 30
  bin MET.MET > 250 and dilepton.m [] 30 50

# Signal bins for stop search
region stopbins
  dileptonpresel
  select Size(electrons) == 1 and Size(muons) == 1
  bin MET.MET [] 125 200 and leptons[0].Pt [] 5 12
  bin MET.MET [] 125 200 and leptons[0].Pt [] 12 20
  bin MET.MET [] 125 200 and leptons[0].Pt [] 20 30
  bin MET.MET [] 200 300 and leptons[0].Pt [] 5 12
  bin MET.MET [] 200 300 and leptons[0].Pt [] 12 20
  bin MET.MET [] 200 300 and leptons[0].Pt [] 20 30
  bin MET.MET > 300 and leptons[0].Pt [] 5 12
  bin MET.MET > 300 and leptons[0].Pt [] 12 20
  bin MET.MET > 300 and leptons[0].Pt [] 20 30

# Dimuon preselection for validation wrt cutflows provided by CMS
# http://cms-results.web.cern.ch/cms-results/public-results/publications/SUS-16-048/index.html
region dimuonpresel
  select ALL
  select size(muons) == 2
  select muons[0].Pt [] 5 30
  select muons[1].Pt [] 5 30
  select muons[0].charge * muons[1].charge == -1
  select dimuon.pT > 3
  select dimuon.m [] 4 50
  select dimuon.m ][ 9 10.5
  select MET.MET [] 125 200
  weight trigger 0.65
  select size(jets) >= 1
  select HT > 100
  select (MET.MET / HT) > 0.6 and (MET.MET / HT) < 1.4
  select size(bjets) == 0
  reject fMTauTau(muons[0], muons[1]) [] 0 160
  select MTl1 < 70 AND MTl2 < 70
