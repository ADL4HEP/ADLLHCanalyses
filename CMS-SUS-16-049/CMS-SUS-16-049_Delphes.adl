#info analysis
#  title "Search for direct production of supersymmetric partners of the top quark in the 
# all-jets final state in proton-proton collisions at sqrt(s) = 13 TeV"
#  experiment CMS
#  id SUS-16-049
#  publication JHEP 10 (2017) 005
#  sqrtS 13.0
#  lumi 35.9
#  arXiv 1707.03316
#  hepdata 
#  doi 10.1007/JHEP10(2017)005

# OBJECTS
object jets
  take Jet
  select pT > 20  
  select abs(Eta) < 2.4

object bjets
  take jets
  select BTag == 1

object MET
  take MissingET

# Boosted objects come after baseline, so not considering for overlap study
object mergedtops
  take FatJet
  select m > 110
  select pT > 400
  select abs(eta) < 2.4
  # select subjet.pT > 20
  # Normally use BDTs. We now use tau32 from razor
  select tau3 / tau2 < 0.46

object mergedWs
  take FatJet
  select m [] 50 110
  select pT > 400
  select abs(eta) < 2.4
  # select subjet.pT > 20
  # Normally use BDTs. We now use tau21 from razor
  select tau2 / tau1 < 0.4

# Involves complex combinations and a BDT, which can be done in an external function.
# Since this is not only used in SR binning, not to be used for the overlap study.
# object resolvedtops
#  take fresolvedtop(jets, bjets, mergedWs, regedtops)

object jetsISR
  take FatJet f
  select pT > 200
  select bTag == 0
  select pT > 300
  select abs(eta) < 2.4
  select dphi(f, MET) >= 2 # can use the phi version to avoid f
  # select dphi(Phi, MET.Phi) >= 2

object muons
  take Muon
  select pT > 5
  select abs(eta) < 2.4
  # relative isolation Irel < 0.10

object electrons
  take Electron
  select pT > 5
  select abs(eta) < 2.4
  # relative isolation Irel < 0.21

object leptons
  take electrons
  take muons

object tau
  take Jet
  select pT > 10
  select abs(eta) < 2.4
  select tauTag == 1

#object vetotracks
# no isolation in Delphes, therefore cannot readily do it in Delphes
#  take Track
#  select pT > 10
#  select abs(eta) < 2.5
#  select sumPt / pT < 0.1 # R = 0.3
  
# VARIABLES
define MTb0 = sqrt( 2*bjets[0].pT * MET*(1-cos(MET.phi - bjets[0].phi )))
define MTb1 = sqrt( 2*bjets[1].pT * MET*(1-cos(MET.phi - bjets[1].phi )))
define MTbmin = min(MTb0, MTb1)
define SMET = MET.pT / sqrt(sum(jets.pT))

# EVENT SELECTION

region baseline
  select MET.pT > 250
  select size(leptons) == 0
  select size(taus) == 0

# The high deltam SRs:
region highdm
  select baseline
  select size(jets) >= 5
  select size(bjets) >= 1
  select dphi(jets[0], MET) > 0.5
  select dphi(jets[1], MET) > 0.5
  select dphi(jets[2], MET) > 0.5
  select dphi(jets[3], MET) > 0.5
  # the rest divided into SRs of MTbmin, Nt, NW, Nresolvedtop, Nb, MET  
  # baseline should suffice for the overlap study.

# The low deltam SRs:
region lowdm
  select baseline
  select size(jets) >= 2
  select size(bjets) == 1 ? mTb0 < 175
  select size(bjets) > 1 ? mTbmin < 175
  select size(jetsISR) > 0
  select dphi(jets[0], MET) > 0.5
  select dphi(jets[1], MET) > 0.15
  select dphi(jets[2], MET) > 0.15
  select SMET > 10
  # the rest divided into SRs of Nj, Nb, NSV, pTISR, pTb, MET.
  # baseline should suffice for the overlap study
